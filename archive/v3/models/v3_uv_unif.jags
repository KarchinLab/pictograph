model {
	for (i in 1:I) {
		for (s in 1:S) {
			w[i,s] ~ dbeta(a[z[i],s], b[z[i],s]) T(0.001,1)
			theta.temp[i,s] <- m[i,s] * w[i,s] / (tcn[i,s] * w[i,s] + 2*(1 - w[i,s]))
			theta[i,s] <- ifelse(theta.temp[i,s] > 1, 1, ifelse(theta.temp[i,s] < 0, 0.001, theta.temp[i,s])) # make sure theta is [0.001, 1]
			y[i, s] ~ dbin(theta[i,s], n[i,s])
		}
		z[i] ~ dcat(pi[1:K])
	}
	
	
	pi[1:K] ~ ddirch(alpha[1:K])
	alpha <- rep(1, K)
	
	# fix order of means in sample 1
	U[1,1] ~ dbeta(1,1)
	for (k in 2:K) {
		U[k,1] ~ dunif(U[k-1,1], 1)
	}
	
	for (s in 2:S) {
		for (k in 1:K) {
			U[k,s] ~ dbeta(1,1)
		}
	}

	for (k in 1:K) {
		for (s in 1:S) {
			V[k,s] ~ dgamma(2,1/10)
			a[k,s] <- U[k,s] * V[k,s]
			b[k,s] <- (1 - U[k,s]) * V[k,s]
		}
	}

}