model {
	for (i in 1:I) { ## for each variant
		for (s in 1:S) {  ## for each sample
			theta.temp[i,s] <- m[i,s] * w[z[i], s] /
                       (tcn[i,s] * w[z[i], s] + 2*(1 - w[z[i], s]))
			theta[i,s] <- ifelse(theta.temp[i,s] > 1, 1, theta.temp[i,s])
			y[i, s] ~ dbin(theta[i,s], n[i,s])
			## posterior predictive distribution
			ystar[i, s] ~ dbin(theta[i, s], n[i, s])
		}
		z[i] ~ dcat(pi[1:K])
	}
	alpha <- rep(1, K)
	pi[1:K] ~ ddirch(alpha[1:K])

	# uniform beta parameters
	a[1] <- 1
	b[1] <- 1
	# beta parameters, mass close to 0
	a[2] <- 1
	b[2] <- 1000

	for (k in 1:K) {
		for (s in 1:S) {
			p[k,s] ~ dbeta(5,2)
			z.p[k,s] ~ dcat(c(p[k,s], 1-p[k,s]))
			w[k,s] ~ dbeta(a[z.p[k,s]], b[z.p[k,s]])
		}
	}
}