model {
	for (i in 1:I) { ## for each variant
		for (s in 1:S) {  ## for each sample
			theta.temp[i,s] <- m[i,s] * w[z[i], s] /
                       (tcn[i,s] * w[z[i], s] + 2*(1 - w[z[i], s]))
			theta[i,s] <- ifelse(theta.temp[i,s] > 1, 1,
                           ifelse(theta.temp[i,s] < 0.001, 0.001,
                                  theta.temp[i,s]))
			y[i, s] ~ dbin(theta[i,s], n[i,s])
			## posterior predictive distribution
			ystar[i, s] ~ dbin(theta[i, s], n[i, s])
		}
		z[i] ~ dcat(pi[1:K])
	}
	alpha <- rep(1, K)
	pi[1:K] ~ ddirch(alpha[1:K])
	for (k in 1:K) {
		for (s in 1:S) {
			U[k,s] ~ dbeta(1,1) T(0.001,1)
			V[k,s] ~ dgamma(2, 1/10)
			a[k,s] <- U[k,s] * V[k,s]
			b[k,s] <- (1 - U[k,s]) * V[k,s]
			w[k,s] ~ dbeta(a[k,s], b[k,s])
		}
	}
}